# Maintainer: Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgname=mesa-git
pkgdesc="an open-source implementation of the OpenGL specification, git version"
pkgver=20.0.0_devel.119494.60f9b45802c
pkgrel=1
arch=('aarch64')
makedepends=('git' 'python-mako' 'xorgproto'
              'libxml2' 'libx11' 'elfutils' 'libxrandr'
              'libgcrypt'  'wayland' 'wayland-protocols' 'meson' 'ninja'
              'llvm' 'clang')
depends=('libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
         'libunwind' 'libglvnd' 'wayland' 'lm_sensors' 'libclc' 'glslang'
         'llvm-libs')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=(mesa=$pkgver-$pkgrel opengl-driver)
conflicts=('mesa')
#options=('!strip')
url="https://www.mesa3d.org"
license=('custom')
source=('mesa::git://anongit.freedesktop.org/mesa/mesa'
        'LICENSE'
)
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')
sha512sums=('SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')

# NINJAFLAGS is an env var used to pass commandline options to ninja
# NOTE: It's your responbility to validate the value of $NINJAFLAGS. If unsure, don't set it.
 
pkgver() {
	cd mesa
	read -r _ver <VERSION
	echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    # although removing _build folder in build() function feels more natural,
    # that interferes with the spirit of makepkg --noextract
    if [  -d _build ]; then
        rm -rf _build
    fi
	cd mesa
#	git revert 3ba16d36c98
}

build() {
	MESON_OPT="-D asm=false"

	arch-meson mesa _build \
		-D b_lto=true \
		-D b_ndebug=true \
		-D platforms=x11,wayland,drm,surfaceless \
		-D dri-drivers= \
		-D gallium-drivers=swrast,virgl,kmsro,panfrost \
		-D vulkan-drivers= \
		-D dri3=true \
		-D egl=true \
		-D gallium-extra-hud=true \
		-D gallium-xa=false \
		-D gallium-xvmc=false \
		-D gbm=true \
		-D gles1=false \
		-D gles2=true \
		-D glvnd=true \
		-D glx=dri \
		-D libunwind=false \
		-D llvm=true \
		-D lmsensors=true \
		-D osmesa=gallium \
		-D shared-glapi=true \
		-D valgrind=false $MESON_OPT

	meson configure _build
	ninja  $NINJAFLAGS -C _build
}


package() {
	DESTDIR="$pkgdir" ninja $NINJAFLAGS -C _build install

# indirect rendering
	ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0
	install -Dt "$pkgdir"/usr/share/licenses/$pkgname "$srcdir"/LICENSE
}
