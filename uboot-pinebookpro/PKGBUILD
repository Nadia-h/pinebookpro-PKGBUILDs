
pkgname=uboot-pinebookpro
pkgver=r62478.908d441fef
pkgrel=1
pkgdesc='Almost upstream u-boot for the Pinebook Pro.'
arch=('aarch64')
url='https://git.eno.space/pbp-uboot.git'
license=('GPL2')
install=${pkgname}.install
makedepends=('make' 'gcc' 'dtc' 'uboot-tools')
replaces=('pinebookpro-uboot')

_atfcommit="22d12c4148c373932a7a81e5d1c59a767e143ac2"
source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#commit=${_atfcommit}"
	"git+https://git.eno.space/pbp-uboot.git"
)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}"/pbp-uboot
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	make -C "${srcdir}"/pbp-uboot pinebook_pro-rk3399_defconfig
	make -C "${srcdir}"/pbp-uboot BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	install -Dm 644 "${srcdir}"/pbp-uboot/idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 "${srcdir}"/pbp-uboot/u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
